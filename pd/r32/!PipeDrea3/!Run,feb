|
| Run file for PipeDream 3.10 (03-May-90) (32-bit)
|
Set PipeDrea3$Dir <Obey$Dir>
|
Set PipeDrea3$Help <PipeDrea3$Dir>.!Help
Set PipeDrea3$Version 3.10
Set PipeDrea3$Web http://croftnuisk.co.uk/coltsoft/pipedream/
Set PipeDrea3$Title PipeDream 3
Set PipeDrea3$Description Integrated spreadsheet and word processor
Set PipeDrea3$Publisher Colton Software
|
IconSprites <PipeDrea3$Dir>.!Sprites
|
| Leave file type sprites alone unless PipeDream 4 hasn't been seen
If "<PipeDream$Dir>"="" Then IconSprites <PipeDrea3$Dir>.!SpritesF3
|
Set File$Type_DDE PDream
SetMacro Alias$@RunType_DDE   Run <PipeDrea3$Dir>.!Run %%*0
SetMacro Alias$@PrintType_DDE Run <PipeDrea3$Dir>.!Run -Print %%*0
|
Set File$Type_D21 PDCmdFil
SetMacro Alias$@RunType_D21   Run <PipeDrea3$Dir>.!Run -Macro %%*0
|
If "<File$Type_DFE>"="" Then Set File$Type_DFE CSV
|
RMEnsure UtilityModule 3.10 Error 0 PipeDream 3 (32-bit) needs RISC OS 3.10 or later
|
If "<Boot$OSVersion>"="" Then RMEnsure UtilityModule 3.50 Error 0 Please install the Universal !Boot before attempting to run PipeDream on older systems. !Boot may be obtained from the RISC OS Open website
If "<Boot$OSVersion>"="" Then Error 0 PipeDream 3 (32-bit) needs Boot$OSVersion to be set
|
| Ensure we have the shared C library and determine its vintage
| NB Don't load a newer version over an older one that may be in RAM
|
Set PipeDrea3$CLibLoaded 1
RMEnsure SharedCLibrary 0.00 Set PipeDrea3$CLibLoaded 0
If PipeDrea3$CLibLoaded=1 Then RMEnsure SharedCLibrary 5.45 Error 0 PipeDream 3 (32-bit) needs SharedCLibrary 5.45 or later but an earlier version is already running. An updated 32-bit !System may be obtained from the RISC OS Open website
Unset PipeDrea3$CLibLoaded
|
RMEnsure SharedCLibrary 5.45 RMLoad System:Modules.CLib
RMEnsure SharedCLibrary 5.45 Error 0 PipeDream 3 (32-bit) needs SharedCLibrary 5.45 or later. An updated 32-bit !System may be obtained from the RISC OS Open website
|
RMEnsure ColourTrans 0.52 RMLoad System:Modules.Colours
RMEnsure ColourTrans 0.52 Error 0 PipeDream 3 (32-bit) needs ColourTrans 0.52 or later. An updated 32-bit !System may be obtained from the RISC OS Open website
|
RMEnsure FPEmulator 4.10 RMLoad System:Modules.FPEmulator
RMEnsure FPEmulator 4.10 Error 0 PipeDream 3 (32-bit) needs FPEmulator 4.10 or later. An updated 32-bit !System may be obtained from the RISC OS Open website
|
If "<PipeDrea3$Path>" = "" Then Obey <PipeDrea3$Dir>.!Path
|
| The higher value is specified as the minimum first so that
| there is sufficient memory to get PipeDream 3 fully running
WimpSlot -min 468k
| The lower value is specified as the maximum last so that
| becomes the initial slot that PipeDream 3 is started with
WimpSlot -max 416k
|
Run <PipeDrea3$Dir>.!RunImage %*0